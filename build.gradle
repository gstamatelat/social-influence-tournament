apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'

Properties props = new Properties()
props.load(new FileInputStream('src/main/resources/app.properties'))

version = props.getProperty("version")

mainClassName = "gr.james.influence.tournament.TournamentMain"
applicationDefaultJvmArgs = ["-Dapp.env=tournament"]

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile 'com.github.gstamatelat:social-influence:80fa4a5354',
            'net.sf.jopt-simple:jopt-simple:4.9',
            'com.github.gstamatelat:ascii-art-table:c0f8c09270',
            'com.github.gstamatelat:Java-Text-Table-Formatter:5949f58d08'
    runtime 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile 'junit:junit:4.12'
}

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task srcZip(type: Zip, group: 'distribution') {
    classifier = 'src'
    from projectDir
    into('social-influence-tournament')
    include 'src/**/*'
    include 'gradle/**/*'
    include 'README.md'
    include 'CHANGELOG.md'
    include 'build.gradle'
    include 'gradlew'
    include 'gradlew.bat'
}
build.dependsOn srcZip

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}
